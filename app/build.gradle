apply plugin: 'com.android.application'
apply plugin: 'io.fabric'


import com.android.build.OutputFile

def keystorePropertiesFile = rootProject.file("app/keystores/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.generateVersionCode()
        versionName rootProject.generateVersionName()

        //support vector drawables for android < 21
        vectorDrawables.useSupportLibrary = true

        //support multi Dex for large library
        multiDexEnabled true

        //support render script for android <19
        renderscriptTargetApi rootProject.ext.minSdkVersion
        renderscriptSupportModeEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    //support data binding
    dataBinding {
        enabled = true
    }
    //increase build speed
    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "2g"
    }
    //enable java 8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/*'
    }
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("keystores/debug.keystore")
            storePassword 'android'
        }
        release {
            storeFile file("keystores/keystore-release.jks")
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            resValue 'string', 'app_name', 'BaseProject-Debug'
        }
        release {
            debuggable false
            minifyEnabled false
//            shrinkResources true
//            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'app_name', 'BaseProject'
        }
    }
    productFlavors {
        develop {
            resValue 'string', 'domainAPI', 'https://animeloversapp.com:3000/api/'
            resValue 'string', 'facebookAppId', '641215556070654'

            flavorDimensions "default"
        }
        staging {
            resValue 'string', 'domainAPI', 'https://animeloversapp.com:3000/api/'
            resValue 'string', 'facebookAppId', '641215556070654'

            flavorDimensions "default"
        }
        production {
            resValue 'string', 'domainAPI', 'https://animeloversapp.com:3000/api/'
            resValue 'string', 'facebookAppId', '641215556070654'

            flavorDimensions "default"
        }
    }

    //split build by abi
    /*splits {
        // Configures multiple APKs based on ABI.
        abi {
            enable true                 // Enables building multiple APKs per ABI.
            reset()
            // Resets the list of ABIs that Gradle should create APKs for to none.

            // Specifies a list of ABIs that Gradle should create APKs for.
            //x86 (optional, very limited number of devices, like Asus Zenfone 2, Genym otion/ Android emulator)
            //x86_64 (optional, very limited number of devices, like Asus Zenfone 2, Genymotion/ Android emulator)
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk false
            // Specifies that we do not want to generate a universal APK that includes all ABIs.
        }
    }*/

    //split version code by abi
    applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    //get app name
                    def appName
                    if (project.hasProperty("applicationName")) {
                        appName = applicationName
                    } else {
                        appName = parent.name
                    }
                    //get abi
                    def abiFilter = output.getFilter(OutputFile.ABI)
                    def abiMultiplier = 0
                    if (abiFilter != null) {
                        abiMultiplier = rootProject.ext.versionCodes.get(abiFilter)
                        output.outputFileName = "${appName}-${variant.productFlavors[0].name}-${abiFilter}-${generateVersionName()}.apk"
                    } else {
                        output.outputFileName = "${appName}-${variant.productFlavors[0].name}-${generateVersionName()}.apk"
                    }
                    output.versionCodeOverride = abiMultiplier * 1000 + android.defaultConfig.versionCode
            }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //social authentication
    implementation project(':social_login')
    //glide
    implementation project(':custom_glide')

    //multiple dex
    implementation "androidx.multidex:multidex:2.0.1"

    //google
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation 'com.google.android.material:material:1.0.0'

    //google services
    implementation "com.google.android.gms:play-services-location:$google_play_service_version"
    implementation "com.google.firebase:firebase-core:$google_play_service_version"
    implementation "com.google.firebase:firebase-analytics:$google_play_service_version"

    //dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    //rx
    implementation "io.reactivex.rxjava2:rxjava:2.2.7"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"

    //okhttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    //room
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"

    //gson
    implementation "com.google.code.gson:gson:2.8.5"

    //logger
    implementation "com.orhanobut:logger:2.2.0"

    //halcyon_logger
    implementation "com.lhalcyon:okhttp-logger-interceptor:1.0.2"

    //dexter - permission requirement
    implementation "com.karumi:dexter:5.0.0"

    //hawk
    implementation "com.orhanobut:hawk:2.0.1"

    //location
    implementation 'pl.charmas.android:android-reactive-location2:2.1@aar'

    //avi refresh layout
    implementation "com.wang.avi:library:2.1.3"

    //snack bar
    implementation 'com.github.matecode:Snacky:1.0.3'

    //Calligraphy font
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'

    //event bus
    implementation 'org.greenrobot:eventbus:3.1.1'

    //leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-2'

    //A utility library for Android to save objects in a Bundle without any boilerplate
    /*implementation('com.evernote:android-state:1.4.1') {
        exclude group: 'androidx.core'
    }
    annotationProcessor 'com.evernote:android-state-processor:1.4.1'*/

    //pretty time
    //implementation 'org.ocpsoft.prettytime:prettytime:4.0.2.Final'

    //Resizer for image
    //implementation 'com.github.hkk595:Resizer:v1.5'
}

apply plugin: 'com.google.gms.google-services'